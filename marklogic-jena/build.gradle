plugins {
  id "java-library"
  id "maven-publish"
  id "com.github.jk1.dependency-license-report" version "2.1"
}

apply plugin: 'com.marklogic.ml-gradle'

group = "com.marklogic"
version = "4.1.0"

java {
  sourceCompatibility = 11
  targetCompatibility = 11
}

dependencies {
  api 'org.apache.jena:jena-arq:4.10.0'

  // Can't bump to 7.0 yet as it removes the deprecated Authentication class, which this project still depends on.
  api 'com.marklogic:marklogic-client-api:6.6.1'

  implementation "org.slf4j:slf4j-api:2.0.16"
  implementation "ch.qos.logback:logback-classic:1.3.14"
  implementation "org.slf4j:jcl-over-slf4j:2.0.16"

  testImplementation 'com.jayway.restassured:rest-assured:2.9.0'
  testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

// Publishing setup - see https://docs.gradle.org/current/userguide/publishing_setup.html .
java {
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  options.overview = "src/main/java/com/marklogic/semantics/jena/overview.html"
}
// Ignores warnings on params that don't have descriptions, which is a little too noisy
javadoc.options.addStringOption('Xdoclint:none', '-quiet')


publishing {
  publications {
    mainJava(MavenPublication) {
      groupId = group
      version = version
      from components.java
      pom {
        name = "${group}:${project.name}"
        description = "Adapter for using MarkLogic with the Apache Jena RDF Framework"
        packaging = "jar"
        url = "https://github.com/marklogic/${project.name}"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = 'MarkLogic Github Contributors'
            email = 'general@developer.marklogic.com'
            organization = 'Github Contributors'
            organizationUrl = 'https://github.com/marklogic/marklogic-jena/graphs/contributors'
          }
        }
        scm {
          url = "git@github.com:marklogic/${project.name}.git"
          connection = "scm:git@github.com:marklogic/${project.name}.git"
          developerConnection = "scm:git@github.com:marklogic/${project.name}.git"
        }
      }
    }
  }
  repositories {
    maven {
      if (project.hasProperty("mavenUser")) {
        credentials {
          username mavenUser
          password mavenPassword
        }
        url publishUrl
        allowInsecureProtocol = true
      } else {
        name = "central"
        url = mavenCentralUrl
        credentials {
          username mavenCentralUsername
          password mavenCentralPassword
        }
      }
    }
  }
}
