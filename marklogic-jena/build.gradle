
apply plugin: 'com.marklogic.ml-gradle'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'maven'


version = '2.0.0-SNAPSHOT'
group   = 'com.marklogic'

dependencies {
    compile('org.apache.jena:jena-arq:3.0.1') {
	   exclude(group: 'org.slf4j')
       exclude(group: 'ch.qos.logback')
       exclude(group: 'log4j')
    }
    compile('com.marklogic:java-client-api:3.0.5') {
	   exclude(group: 'org.slf4j')
       exclude(group: 'ch.qos.logback')
    }
   
	compile("org.slf4j:slf4j-api:$slf4jVersion")
	compile "ch.qos.logback:logback-classic:$logbackVersion"
	compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"

	testCompile 'com.jayway.restassured:rest-assured:2.5.0'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'junit:junit:4.12'
}

sourceSets {
	test {
		java {
			srcDir 'src/test/resources'
		}
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.overview = "src/main/java/com/marklogic/semantics/jena/overview.html"
}

Node pomCustomizations = new NodeBuilder(). project {
        name 'marklogic-jena'
        packaging 'jar'
        textdescription 'Adapter for using MarkLogic with the jena RDF Framework'
        url 'https://github.com/marklogic/marklogic-jena'

        scm {
            url 'git@github.com:marklogic/marklogic-jena.git'
                connection 'scm:git@github.com:marklogic/marklogic-jena.git'
                developerConnection 'scm:git@github.com:marklogic/marklogic-jena.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
 
        developers {
            developer {
                name 'MarkLogic'
                    email 'java-sig@marklogic.com'
                    organization 'MarkLogic'
                    organizationUrl 'https://www.marklogic.com'
            }
            developer {
                name 'MarkLogic Github Contributors'
                    email 'general@developer.marklogic.com'
                    organization 'Github Contributors'
                    organizationUrl 'https://github.com/marklogic/marklogic-jena/graphs/contributors'
            }
        }
}

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
        
      pom.withXml { 
        asNode().append(pomCustomizations.developers)
        asNode().append(pomCustomizations.name)
        asNode().append(pomCustomizations.packaging)
        asNode().append(pomCustomizations.url)
        asNode().append(pomCustomizations.scm)
        asNode().append(pomCustomizations.licenses)
        asNode().appendNode("description", pomCustomizations.textdescription.text())
      }

      artifact sourcesJar 

      artifact javadocJar 

    }
  }
  repositories {
    maven {
      url publishUrl
    }
  }
}
